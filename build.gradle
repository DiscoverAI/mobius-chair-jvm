plugins {
    id 'scala'
    id 'maven'
    id 'signing'
    id 'com.github.maiflai.scalatest' version '0.23'
    id 'net.researchgate.release' version '2.4.0'
    id 'io.codearte.nexus-staging' version '0.11.0'
}

group = "com.github.meandor"

def scalaVersion = "2.11"

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion.+"
    compile 'org.apache.hadoop:hadoop-client:3.1.1'

    testCompile "org.scalatest:scalatest_$scalaVersion:3.0.5"
    testRuntime "org.scala-lang.modules:scala-xml_$scalaVersion:+"
    testRuntime 'org.pegdown:pegdown:1.4.2'
}

repositories {
    mavenCentral()
    mavenLocal()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'mobius-chair'
                packaging 'jar'
                description 'Scala library for managing versions of datasets.'
                url 'https://github.com/meandor/mobius-chair'

                scm {
                    url 'scm:git@github.com:meandor/mobius-chair.git'
                    connection 'scm:git@github.com:meandor/mobius-chair.git'
                    developerConnection 'scm:git@github.com:meandor/mobius-chair.git'
                }

                licenses {
                    license {
                        name 'Apache License'
                        url 'https://github.com/meandor/transporter/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'dschruhl'
                        name 'Daniel Schruhl'
                        email 'danielschruhl@gmail.com'
                    }
                }
            }
        }
    }
}
